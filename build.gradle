buildscript {
    ext {
        lombokVersion = '1.18.4'
        springBootVersion = '2.1.1.RELEASE'
        springCloudVersion = 'Finchley.SR2'
        swaggerVersion = '2.9.2'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '2.1.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url 'https://dl.bintray.com/palantir/releases'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

checkstyle {
	configFile = new File(rootDir, "checkstyle.xml")
}

sourceSets {
	componentTest {
		java.srcDir file('src/component-test/java')
		resources.srcDir file('src/component-test/resources')
	}
}

task componentTest(type: Test) {
	testClassesDirs = sourceSets.componentTest.output.classesDirs
	classpath = sourceSets.componentTest.runtimeClasspath
}

componentTest.dependsOn test
check.dependsOn componentTest
jar.dependsOn check

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'

    compile "io.springfox:springfox-swagger2:${swaggerVersion}"
    compile "io.springfox:springfox-swagger-ui:${swaggerVersion}"

    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:3.11.1"
    testCompile "io.rest-assured:spring-mock-mvc:3.2.0"
    testCompile "org.mockito:mockito-core:2.23.4"
    testCompile "org.springframework.boot:spring-boot-starter-test"

    componentTestCompile sourceSets.main.output
    componentTestCompile configurations.compile
    componentTestCompile 'junit:junit:4.12'
    componentTestCompile 'org.assertj:assertj-core:3.11.1'
    componentTestCompile 'com.palantir.docker.compose:docker-compose-rule-junit4:0.34.0'
    componentTestCompile 'io.rest-assured:rest-assured:3.1.0'
}

mainClassName = 'lt.rieske.skeleton.Application'
