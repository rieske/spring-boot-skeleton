plugins {
    id "java"
    id "org.springframework.boot" version "2.5.0"
    id "jacoco"
    id "checkstyle"
    id "com.github.kt3k.coveralls" version "2.10.2"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

sourceSets {
	componentTest {
		java.srcDir file("src/component-test/java")
		resources.srcDir file("src/component-test/resources")
	}
}

task componentTest(type: Test)

tasks {
    wrapper {
        gradleVersion = "5.6.4"
    }

    bootJar {
        archiveFileName = "skeleton.jar"
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    checkstyle {
        configFile = new File(rootDir, "checkstyle.xml")
    }

    test {
        useJUnitPlatform()
    }

    componentTest {
        dependsOn(test)
        testClassesDirs = sourceSets.componentTest.output.classesDirs
        classpath = sourceSets.componentTest.runtimeClasspath
        testLogging.showStandardStreams = true
        testLogging.exceptionFormat = "full"
        exclude("**/**Test.class")
    }

    check.dependsOn(componentTest)
}

dependencies {
    compileOnly("org.projectlombok:lombok:1.18.12")
    annotationProcessor("org.projectlombok:lombok:1.18.12")

    implementation(platform("org.springframework.boot:spring-boot-dependencies:2.3.4.RELEASE"))

    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-actuator")

    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testImplementation("io.rest-assured:spring-mock-mvc:4.3.1")

    componentTestImplementation(configurations.implementation)
    componentTestImplementation("org.testcontainers:testcontainers:1.13.0")
    componentTestImplementation("io.rest-assured:rest-assured:4.3.1")
}

