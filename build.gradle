buildscript {
    ext {
        lombokVersion = '1.18.4'
        springBootVersion = '2.1.1.RELEASE'
        springCloudVersion = 'Finchley.SR2'
        swaggerVersion = '2.9.2'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '2.1.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'jacoco'
    id 'checkstyle'
    id 'com.github.kt3k.coveralls' version '2.8.4'
}

sourceCompatibility = 11

repositories {
    mavenCentral()
    maven {
        url 'https://dl.bintray.com/palantir/releases'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

checkstyle {
	configFile = new File(rootDir, "checkstyle.xml")
}

sourceSets {
	componentTest {
		java.srcDir file('src/component-test/java')
		resources.srcDir file('src/component-test/resources')
	}
}

task componentTest(type: Test) {
	testClassesDirs = sourceSets.componentTest.output.classesDirs
	classpath = sourceSets.componentTest.runtimeClasspath
}

componentTest.dependsOn test
check.dependsOn componentTest
jar.dependsOn check

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation "io.springfox:springfox-swagger2:${swaggerVersion}"
    implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"

    testImplementation "junit:junit:4.12"
    testImplementation "org.assertj:assertj-core:3.11.1"
    testImplementation "io.rest-assured:spring-mock-mvc:3.2.0"
    testImplementation "org.mockito:mockito-core:2.23.4"
    testImplementation "org.springframework.boot:spring-boot-starter-test"

    componentTestImplementation configurations.implementation
    componentTestImplementation 'junit:junit:4.12'
    componentTestImplementation 'org.assertj:assertj-core:3.11.1'
    componentTestImplementation 'com.palantir.docker.compose:docker-compose-rule-junit4:0.34.0'
    componentTestImplementation 'io.rest-assured:rest-assured:3.1.0'
}

mainClassName = 'lt.rieske.skeleton.Application'

wrapper {
    gradleVersion = "5.6.4"
}
