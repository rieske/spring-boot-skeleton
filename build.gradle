buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
    	classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE'
    	classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'com.github.kt3k.coveralls'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

jar.baseName = 'spring-mvc-rest'

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
 }

checkstyle {
	toolVersion = "6.0"
	configFile = new File(rootDir, "checkstyle.xml")
}

findbugs {
    sourceSets = [sourceSets.main, sourceSets.test]
}

sourceSets {
	componentTest {
		java.srcDir file('src/component-test/java')
		resources.srcDir file('src/component-test/resources')
	}
}

task componentTest(type: Test) {
	testClassesDir = sourceSets.componentTest.output.classesDir
	classpath = sourceSets.componentTest.runtimeClasspath
}

componentTest {
    jacoco {
        append = true
        destinationFile = file("$buildDir/jacoco/test.exec")
    }
}

componentTest.dependsOn test
check.dependsOn componentTest
jar.dependsOn check

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "com.mangofactory:swagger-springmvc:1.0.2"

    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:3.3.0"
    testCompile "com.jayway.restassured:spring-mock-mvc:2.8.0"
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile "org.springframework.boot:spring-boot-starter-test"

    componentTestCompile sourceSets.main.output
    componentTestCompile configurations.compile
    componentTestCompile "org.springframework.boot:spring-boot-starter-test"
    componentTestCompile "com.jayway.restassured:rest-assured:2.8.0"
}

task wrapper(type: Wrapper) { gradleVersion = '2.10' }
